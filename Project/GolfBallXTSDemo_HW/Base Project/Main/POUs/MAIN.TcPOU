<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="MAIN" Id="{0790caa7-8a3f-4f91-b0f8-9ebb3db3858d}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM MAIN
VAR
	// Machine Stations
	OrientStation:		OrientStation;
	BallSeatStation:	BallSeatStation;
	PrintStation:		PrintStation;
	SimTimers:			SimTimers_typ;

	// ========= System Control / FSM Variables =========
	
	MainCommands 		: MainCommands_typ; 	// command struct for cycling thru the state machine
	MainState    		: MainState_enum;   	// enumeration for the Main state machine

	// ========= Linked NC Variables & References =========
	
	Mover    			: ARRAY[0..GVL.NUM_MOVERS - 1] OF Mover; 	// instantiation of Mover objects. Matches the number of movers on the system
    GroupRef 			: AXES_GROUP_REF;                       	 // linked to the Collision Avoidance group and passed cyclically to every Mover object

	// ========= Objectives & Parameters =========

    MoverList    		: MoverList;
    ParameterSet 		: MotionParameters_typ;

    Station         	: ARRAY[0..GVL.NUM_STATIONS - 1] OF Station;
	MultiStation		: ARRAY[0..GVL.NUM_MULTISTATIONS - 1] OF MultiStation;
    Zone            	: ARRAY[0..0] OF Zone;

    // ========= Example Application Variables =========

    lowVelocity    		: LREAL := 200;  	// mm/s
    mediumVelocity 		: LREAL := 800;  	// mm/s
    highVelocity   		: LREAL := 1200; 	// mm/s

    // ========= Helper, State, & Index Variables =========
	
    i                	: UINT; 	// index variable; gets reused in various FOR loops
	j					: UINT;		// index variable; gets reused in various FOR loops
    n                	: UINT; 	// secondary index variable, for enabling movers
    allMoversEnabled 	: BOOL; 	// helper flag for verifying enable status of all movers
	allMoverTracksEnabled : BOOL;	// helper flag for verifying track active status of all movers
	allMoversDisabled	: BOOL;		// helper flag for verifying disable status of all movers
	allMoversHalted		: BOOL;		// helper flag for verifying halt status of all movers

    StationTimer 		: ARRAY[0..GVL.NUM_STATIONS-1] OF TON; // timer blocks, for station dwells	
    MultiStationTimer 	: ARRAY[0..GVL.NUM_MultiSTATIONS-1] OF TON; // timer blocks, for station dwells	
	
	// ========= XTS Utility Diagnostics =========
	
	VisuDiag						: FB_VisuXtsDiag;
	fbXtsEnvironment				: FB_TcIoXtsEnvironment;
	fbXtsEnvironmentVisu			: FB_XtsEnvironmentVisu;
	stXtsEnvironmentConfiguration	: ST_XtsEnvironmentConfiguration;
	nEnvironmentState				: INT;
	
	// ======== Track management ========
	Track		: ARRAY [0..GVL.NUM_TRACKS] OF Track;

	// IO
	IO: IO_typ;

END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* ======================================================================================

--- XTS Starter Project ---

This MAIN (PRG) is intended to serve as an example, which can be modified to suit your specific application.

No programming changes should be necessary inside the provided XTS objects / function block definitions.
They are intended to be simply instantiated and controlled via the provided methods and properties.
However the objects are provided as open-source for modification and extension as needed.

Documentation for these objects can be found online at:
https://beckhoff-usa-community.github.io/XTS_Base/

Bug reports, new feature requests, and general feedback is always welcome on our Github page.
For any other questions, please reach out to your local Application Engineer.

- Beckhoff USA Advanced Applications Group

====================================================================================== *)
_AppInfo.KeepOutputsOnBP := TRUE;
CASE MainState OF
    MS_DISABLED: // --------------------------------------------------------- IDLE STATE
		(*Wait here until an operator issues a MainCommand to Enable the system. 
		Disallow Start and Stop commands until the system has been Enabled*)

        MainCommands.Start		:= FALSE;
		MainCommands.Stop		:= FALSE;
		
        IF MainCommands.Enable THEN
            MainCommands.Disable := FALSE;
            MainState            := MS_INITIALIZING_MOVERS;
        END_IF

    MS_INITIALIZING_MOVERS: // --------------------------------------------- SET MOVER MOTION PARAMETERS
		(*Initialize Mover motion parameters via a Parameter Set. Once complete, step
		forward to begin Enabling movers on the system*)
		
		ParameterSet.Jerk         := 1E5; // mm/s3
		ParameterSet.Acceleration := 1E4; // mm/s2
		ParameterSet.Deceleration := 1E4; // mm/s2
		ParameterSet.Velocity     := 1E3; // mm/s
		ParameterSet.Direction    := mcDirectionPositive;

        FOR i := 0 TO GVL.NUM_MOVERS - 1 DO
            Mover[i].MotionParameters := ParameterSet;
        END_FOR
		
		IF MainCommands.Disable THEN
			MainCommands.Enable		:= FALSE;
			MainState				:= MS_DISABLED;
		ELSIF MainCommands.Stop THEN
			MainCommands.Stop		:= FALSE;
			MainCommands.Disable	:= TRUE;
			MainCommands.Start		:= FALSE;
			MainState				:= MS_DISABLED;
		ELSIF MainCommands.Enable THEN
        	MainState 				:= MS_ONESHOT_ENABLE;
		END_IF

	MS_ONESHOT_ENABLE:	// -------------------------------------------------- SINGLE-CYCLE ISSUE ENABLE COMMANDS
		(*Begin Enabling movers by calling Mover.Enable(). Methods in this project
		are intended to be called once, although they will function correctly if called repeatedly*)

		FOR i := 0 TO GVL.NUM_MOVERS - 1 DO
			Mover[i].Enable();
		END_FOR

		MainState		:= MS_ENABLING_MOVERS;
	
    MS_ENABLING_MOVERS: // -------------------------------------------------- RESETTING AND ENABLING AXES
		(*Wait here until all movers have Enabled. This means that the Axis has
		fully energized, and also that all Collision Avoidance grouping and Mover 1 detection
		has finished. If the state machine gets stuck in this state, check the Error output
		of an individual Mover to see what the issue is. A common cause is forgetting to link
		Mover NC Axes to the AXIS_REF inside the PLC*)

        allMoversEnabled := TRUE; // default ...

        FOR i := 0 TO GVL.NUM_MOVERS - 1 DO
            IF Mover[i].Ready = FALSE THEN
                allMoversEnabled 		:= FALSE;
            END_IF
        END_FOR

        IF allMoversEnabled THEN
			IF MainCommands.Disable THEN
				MainCommands.Enable		:=FALSE;
            	MainState				:= MS_ONESHOT_DISABLE;
			ELSIF MainCommands.Stop	 THEN
				MainCommands.Start		:=FALSE;
				MainState				:= MS_ONESHOT_STOP;
			ELSIF MainCommands.Enable THEN
	        	MainState 				:= MS_INITIALIZING_OBJECTIVES;
			END_IF
        END_IF

    MS_INITIALIZING_OBJECTIVES: // --------------------------------------------- WRITING OBJECTIVE PARAMETERS
		(*Initialize Station positions, Position Trigger positions, Zones etc. *)

        // Initialize station positions
        Station[GVL.STN_ORIENT].Position	:= 800;
        Station[GVL.STN_BALLSEAT].Position	:= 900;
        Station[GVL.STN_VISION].Position	:= 2125;
        Station[GVL.STN_KICKOUT].Position	:= 2375;

		MultiStation[GVL.MULTISTN_PRINT].PositionTrigger.Position	:= 1400;
		MultiStation[GVL.MULTISTN_PRINT].Station[0].Position		:= 1621;
		MultiStation[GVL.MULTISTN_PRINT].Station[1].Position		:= 1707;
		MultiStation[GVL.MULTISTN_PRINT].Station[2].Position		:= 1793;
		MultiStation[GVL.MULTISTN_PRINT].Station[3].Position		:= 1879;

		MultiStation[GVL.MULTISTN_TRAYLOAD].PositionTrigger.Position	:= 2900;
		MultiStation[GVL.MULTISTN_TRAYLOAD].Station[0].Position			:= 121;
		MultiStation[GVL.MULTISTN_TRAYLOAD].Station[1].Position			:= 207;
		MultiStation[GVL.MULTISTN_TRAYLOAD].Station[2].Position			:= 293;
		MultiStation[GVL.MULTISTN_TRAYLOAD].Station[3].Position			:= 379;

        Zone[0].StartPosition := 0;
        Zone[0].EndPosition   := 3000;

		// give all tracks their ID number
		FOR i := 0 TO GVL.NUM_TRACKS DO
			Track[i].Id := UINT_TO_INT(i);
		END_FOR
		
		// If a single logical track is used (typically aa closed loop) then tracks to not need to be initialized
		Track[0].OTCID := 0;				// special "absolute reference" case for a single closed loop
//		Track[1].OTCID:= 16#010100A0;		// value copied from XtsProcessingUnit1 > Track x > Object > Object Id field

		MainState := MS_ACTIVATE_TRACKS;
		
	MS_ACTIVATE_TRACKS:
		// activate all movers on track[0]
		FOR i := 0 TO GVL.NUM_MOVERS - 1 DO
            Mover[i].ActivateTrack(Track[0]);
        END_FOR
		
		MainState := MS_WAIT_FOR_TRACKS;
	
	MS_WAIT_FOR_TRACKS:
		// wait for all movers to activate their tracks
		
		allMoverTracksEnabled := TRUE;
		FOR i := 0 TO GVL.NUM_MOVERS - 1 DO
			IF (NOT Mover[i].IsTrackReady) THEN
            	allMoverTracksEnabled := FALSE;
			END_IF;
        END_FOR
		IF (allMoverTracksEnabled) THEN
			MainState := MS_ENABLED;	
		END_IF

    MS_ENABLED: // ------------------------------------------------------------- MOVERS ENABLED, STOPPED
		(*In this state, Movers should be energized and holding their position. They should wait
		until a Start command is issued. This is also a good state to include basic Recovery steps.
		Our example shows a simple use-case involving Zones for localized recovery*)

		MainCommands.Stop	:= FALSE;
	
        IF MainCommands.Start THEN

            // ===== RECOVERY LOGIC ==== //

            // Register all movers with all Zones, for recovery
            FOR i := 0 TO GVL.NUM_MOVERS - 1 DO
				FOR j := 0 TO GVL.NUM_ZONES - 1 DO
					Zone[j].RegisterMover( Mover[i] );
				END_FOR
            END_FOR

            Zone[0].CurrentMoverList.SetAllVelocity(1500).MoveAllToStation(Station[GVL.STN_ORIENT]);

            // ===== END =====

            MainState			:= MS_RUN;

        ELSIF MainCommands.Disable THEN
            MainCommands.Enable := FALSE;
            MainState           := MS_ONESHOT_STOP;
        END_IF

    MS_RUN: // --------------------------------------------------------------- OPERATING
		(*Mover routing logic should be written here to define the system's operating behavior.
		Station implementations, Position Trigger logic, etc. In general, you can remove the example code
		here down to the short line break that looks like this: // ============= *)

		OrientStation.Run	:= TRUE;
		BallSeatStation.Run	:= TRUE;
		PrintStation.Run	:= TRUE;
	
        // Vision Station Logic	
		// Station specific logic to be moved to FUB
        IF Station[GVL.STN_VISION].MoverInPosition THEN
            StationTimer[GVL.STN_VISION](IN := TRUE, PT := T#250MS);
            IF StationTimer[GVL.STN_VISION].Q THEN
                Station[GVL.STN_VISION].CurrentMover.MoveToStation(Station[GVL.STN_ORIENT]);
            END_IF
        ELSE
            StationTimer[GVL.STN_VISION](IN := FALSE);
        END_IF

		// Kickout Station Logic
		// Station specific logic to be moved to FUB
        IF Station[GVL.STN_KICKOUT].MoverInPosition THEN
            StationTimer[GVL.STN_KICKOUT](IN := TRUE, PT := T#1000MS);
            IF StationTimer[GVL.STN_KICKOUT].Q THEN
				// maybe write a function to remove all ball data when ball is removed?
//				Station[GVL.STN_KICKOUT].CurrentMover.Payload.HasBall		:= FALSE;	
//				Station[GVL.STN_KICKOUT].CurrentMover.Payload.Reject		:= FALSE;
//				Station[GVL.STN_KICKOUT].CurrentMover.Payload.BallSeated	:= FALSE;
                Station[GVL.STN_KICKOUT].CurrentMover.MoveToStation(Station[GVL.STN_ORIENT]);
            END_IF
        ELSE
            StationTimer[GVL.STN_KICKOUT](IN := FALSE);
        END_IF

		// Tray Load Station Logic
		IF MultiStation[GVL.MULTISTN_TRAYLOAD].Station[0].MoverInPosition THEN
			MultiStationTimer[GVL.MULTISTN_TRAYLOAD](IN := TRUE, PT := T#1500MS);
			IF MultiStationTimer[GVL.MULTISTN_TRAYLOAD].Q THEN
				FOR i := 0 TO 3 DO
					MultiStation[GVL.MULTISTN_TRAYLOAD].Station[i].CurrentMover.MoveToStation(Station[GVL.STN_ORIENT]);
				END_FOR
				MultiStationTimer[GVL.MULTISTN_TRAYLOAD](IN := FALSE);
			END_IF
		ELSE
			MultiStationTimer[GVL.MULTISTN_TRAYLOAD](IN := FALSE);
		END_IF

		// Example call to XTS Utility library. See Diagnostics section of docs for more info
		(*
		myMoverMileage	:= fbXtsEnvironment.XpuTcIo(1).MoverTcIo(2).GetDistanceDriven();
		*)
		
		// =============

        IF MainCommands.Stop THEN
            MainCommands.Start 	:= FALSE;
            MainState         	:= MS_ONESHOT_STOP;
        ELSIF MainCommands.Disable THEN
            MainCommands.Enable := FALSE;
            MainState           := MS_ONESHOT_STOP;
        END_IF
		
	MS_ONESHOT_STOP:	// --------------------------------------------------- SINGLE-CYCLE ISSUE HALT COMMANDS
		(*Issue a halt command, then change states right away to wait for Movers to carry out the instruction*)
	
		FOR i := 0 TO GVL.NUM_MOVERS - 1 DO
			Mover[i].Halt();
		END_FOR

		OrientStation.Run	:= FALSE;
		BallSeatStation.Run	:= FALSE;
		PrintStation.Run	:= FALSE;
	
		MainState					:= MS_STOPPING;
	
    MS_STOPPING: // ---------------------------------------------------------- STOPPING MOVERS
		(*Movers are coming to a stop here in this state*)

		// Stop all movers
		allMoversHalted				:= TRUE;
		FOR i := 0 TO GVL.NUM_MOVERS - 1 DO
			Mover[i].AxisReference.ReadStatus();
			IF Mover[i].AxisReference.Status.Moving THEN
				allMoversHalted		:= FALSE;
			END_IF
		END_FOR
		
		MainCommands.Start			:= FALSE;
 
		IF allMoversHalted THEN
           	MainState 				:= MS_STOPPED;
		END_IF
        

    MS_STOPPED: // ------------------------------------------------------------ Stop Command has been sent, hold here
		(*Movers are now stopped, and further Main Commands can be issued to the system*)

        IF MainCommands.Disable THEN
            MainCommands.Enable 	:= FALSE;
			MainCommands.Start		:= FALSE;
            MainState           	:= MS_ONESHOT_DISABLE;
        ELSIF MainCommands.Start THEN
            MainCommands.Stop 		:= FALSE;
            MainState         		:= MS_ENABLING_MOVERS;
        END_IF

        // Unregister everything from everything
        FOR i := 0 TO GVL.NUM_STATIONS - 1 										DO Station[i].UnregisterAll();			END_FOR
		FOR i := 0 TO GVL.NUM_MULTISTATIONS - 1 								DO MultiStation[i].UnregisterAll();		END_FOR
//      FOR i := 0 TO SIZEOF(PositionTrigger) / SIZEOF(PositionTrigger[0]) - 1	DO PositionTrigger[i].UnregisterAll();	END_FOR
//      FOR i := 0 TO SIZEOF(SpeedTrigger) / SIZEOF(SpeedTrigger[0]) - 1		DO SpeedTrigger[i].UnregisterAll();		END_FOR
		FOR i := 0 TO SIZEOF(Zone) / SIZEOF(Zone[0]) - 1 						DO	Zone[i].UnregisterAll();			END_FOR
	
	MS_ONESHOT_DISABLE:	// ------------------------------------------------------- SINGLE CYCLE ISSUE DISABLE COMMANDS
		(*Mover disable commands are issued here, followed by a state tranisition to await completion of the Disabling*)
		FOR i := 0 TO GVL.NUM_MOVERS DO
			Mover[i].Disable();
		END_FOR

		MainState					:=	MS_DISABLING;
	
    MS_DISABLING: // --------------------------------------------------- DISABLING MOVERS
		(*Movers are Disabling. During this step, we also Unregister movers from relevant Objectives
		to ensure proper startup behavior*)

        // Unregister everything from everything
        FOR i := 0 TO GVL.NUM_STATIONS - 1 										DO Station[i].UnregisterAll();			END_FOR
		FOR i := 0 TO GVL.NUM_MULTISTATIONS - 1 								DO MultiStation[i].UnregisterAll();		END_FOR
//      FOR i := 0 TO SIZEOF(PositionTrigger) / SIZEOF(PositionTrigger[0]) - 1	DO PositionTrigger[i].UnregisterAll();	END_FOR
//      FOR i := 0 TO SIZEOF(SpeedTrigger) / SIZEOF(SpeedTrigger[0]) - 1		DO SpeedTrigger[i].UnregisterAll();		END_FOR
		FOR i := 0 TO SIZEOF(Zone) / SIZEOF(Zone[0]) - 1 						DO	Zone[i].UnregisterAll();			END_FOR

		allMoversDisabled				:= TRUE;
		FOR i := 0 TO gvl.NUM_MOVERS-1 DO
			IF NOT Mover[i].AxisReference.Status.Disabled THEN 
				allMoversDisabled		:= FALSE;
			END_IF
		END_FOR
 
        IF allMoversDisabled THEN
            MainState					:= MS_DISABLED;
        END_IF

    MS_ERROR: // --------------------------------------------------- ERRORSTATE
		(*A reset command can be included here to clear Mover errors, etc. and return
		to a controlled state*)

    MS_DEBUG: // ------------------------------------------------------- DEV USE ONLY
		(*This state is intentionally left blank, 
		for optional use by a developer during programming*)

END_CASE;

// Station Actions
ACT_Orient();
ACT_BallSeat();
ACT_Print();
ACT_IOSim();

// Initialize and update diagnostic features
InitializeUtilityLibrary();

// Call all mover function blocks cyclically
FOR i := 0 TO GVL.NUM_MOVERS - 1 DO
    Mover[i].Cyclic(GroupRef);
	Mover[i].AxisReference.ReadStatus();
END_FOR;

// Call all station function blocks cyclically
FOR i := 0 TO GVL.NUM_STATIONS - 1 DO
    Station[i].Cyclic();
END_FOR

// Call all multi station function blocks cyclically
FOR i := 0 TO GVL.NUM_MULTISTATIONS - 1 DO
	MultiStation[i].Cyclic();
END_FOR
]]></ST>
    </Implementation>
    <Folder Name="Stations" Id="{f025fb0e-005b-0773-0290-1b0d76ec6369}" />
    <Action Name="ACT" Id="{2407f9f6-3a9c-0a23-3122-b9df364660e0}">
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Action>
    <Action Name="ACT_BallSeat" Id="{d30c79e2-8303-0990-143c-9c22e976d6a9}" FolderPath="Stations\">
      <Implementation>
        <ST><![CDATA[// INIT
BallSeatStation.rStation		REF= Station[GVL.STN_BALLSEAT];
BallSeatStation.rIO				REF= IO.BallSeat;
BallSeatStation.rPrintStation	REF= MultiStation[GVL.MULTISTN_PRINT];

// Inputs
BallSeatStation.Enable	:= MainCommands.Enable;

// FB CALLS
BallSeatStation();]]></ST>
      </Implementation>
    </Action>
    <Action Name="ACT_IOSim" Id="{6ed4ba4c-34e2-0ad2-01bd-6b8a12409293}" FolderPath="Stations\">
      <Implementation>
        <ST><![CDATA[// Orient
SimTimers.TON_Orient_OpenBallClamp(IN := IO.Orient.Outputs.BallClampOpen AND NOT IO.Orient.Outputs.BallClampClose, PT := T#150MS);
SimTimers.TON_Orient_CloseBallClamp(IN := IO.Orient.Outputs.BallClampClose AND NOT IO.Orient.Outputs.BallClampOpen, PT := T#150MS);
SimTimers.TON_Orient_LoadBall(IN := IO.Orient.Outputs.LoadBall, PT := T#700MS);

IF SimTimers.TON_Orient_CloseBallClamp.Q THEN		IO.Orient.Inputs.BallClampClosed	:= TRUE;	IO.Orient.Inputs.BallClampOpened	:= FALSE;	END_IF
IF SimTimers.TON_Orient_OpenBallClamp.Q THEN		IO.Orient.Inputs.BallClampOpened	:= TRUE;	IO.Orient.Inputs.BallClampClosed	:= FALSE;	END_IF
IO.Orient.Inputs.BallLoadComplete					:= SimTimers.TON_Orient_LoadBall.Q;

// Ball Seater
SimTimers.TON_BallSeat_OpenBallClamp(IN := IO.BallSeat.Outputs.BallClampOpen AND NOT IO.BallSeat.Outputs.BallClampClose, PT := T#150MS);
SimTimers.TON_BallSeat_CloseBallClamp(IN := IO.BallSeat.Outputs.BallClampClose AND NOT IO.BallSeat.Outputs.BallClampOpen, PT := T#150MS);
SimTimers.TON_BallSeat_ExtendBallSeater(IN := IO.BallSeat.Outputs.ExtendBallSeater AND NOT IO.BallSeat.Outputs.RetractBallSeater, PT := T#150MS);
SimTimers.TON_BallSeat_RetractBallSeater(IN := IO.BallSeat.Outputs.RetractBallSeater AND NOT IO.BallSeat.Outputs.ExtendBallSeater, PT := T#150MS);

IF SimTimers.TON_BallSeat_CloseBallClamp.Q THEN		IO.BallSeat.Inputs.BallClampClosed		:= TRUE;	IO.BallSeat.Inputs.BallClampOpened		:= FALSE;	END_IF
IF SimTimers.TON_BallSeat_OpenBallClamp.Q THEN		IO.BallSeat.Inputs.BallClampOpened		:= TRUE;	IO.BallSeat.Inputs.BallClampClosed		:= FALSE;	END_IF
IF SimTimers.TON_BallSeat_ExtendBallSeater.Q THEN	IO.BallSeat.Inputs.BallSeatExtended		:= TRUE;	IO.BallSeat.Inputs.BallSeatRetracted	:= FALSE;	END_IF
IF SimTimers.TON_BallSeat_RetractBallSeater.Q THEN	IO.BallSeat.Inputs.BallSeatRetracted	:= TRUE;	IO.BallSeat.Inputs.BallSeatExtended		:= FALSE;	END_IF

// Printer
SimTimers.TON_Print_PrintComplete(IN := IO.Print.Outputs.Print, PT := T#1200MS);

IO.Print.Inputs.PrintActive		:= SimTimers.TON_Print_PrintComplete.IN AND NOT SimTimers.TON_Print_PrintComplete.Q;
IO.Print.Inputs.PrintComplete	:= SimTimers.TON_Print_PrintComplete.Q;

]]></ST>
      </Implementation>
    </Action>
    <Action Name="ACT_Orient" Id="{32224cff-87eb-095f-1e16-1fc8acfca016}" FolderPath="Stations\">
      <Implementation>
        <ST><![CDATA[// INIT
OrientStation.rStation		REF= Station[GVL.STN_ORIENT];
OrientStation.rIO			REF= IO.Orient;
OrientStation.rBallSeatStn	REF= Station[GVL.STN_BALLSEAT];
OrientStation.rKickoutStn	REF= Station[GVL.STN_KICKOUT];

// Inputs
OrientStation.Enable	:= MainCommands.Enable;

// FB CALLS
OrientStation();]]></ST>
      </Implementation>
    </Action>
    <Action Name="ACT_Print" Id="{2203369a-7a83-048d-1341-ae755014cdc1}" FolderPath="Stations\">
      <Implementation>
        <ST><![CDATA[// INIT
PrintStation.rMultiStn			REF= MultiStation[GVL.MULTISTN_PRINT];
PrintStation.rIO				REF= IO.Print;
PrintStation.rVisionStation		REF= Station[GVL.STN_VISION];
PrintStation.rKickoutStation	REF= Station[GVL.STN_KICKOUT];

// Inputs
PrintStation.Enable		:= MainCommands.Enable;
PrintStation.NumColor	:= 2;
PrintStation.NumIndex	:= 2;

// FB CALLS
PrintStation();]]></ST>
      </Implementation>
    </Action>
    <Action Name="InitializeUtilityLibrary" Id="{4ac3b602-bbee-0af7-24db-b9d89bbd3ae5}">
      <Implementation>
        <ST><![CDATA[


CASE nEnvironmentState OF
	0:	// ----------------------------------------- Enable Init Items
	
		// Enable init Info Server if needed
		stXtsEnvironmentConfiguration.bEnableInitInfoServer		:= TRUE;
		
		// Enable init CA Group if needed
		stXtsEnvironmentConfiguration.bEnableInitCaGroup		:= TRUE;
		
		// Set configuration of XtsUtility init
		fbXtsEnvironment.P_XtsEnvironmentConfiguration			:= stXtsEnvironmentConfiguration;
		
		// Next step
		nEnvironmentState	:= 1;
		
	1:	// ------------------------------------------ Init Environment
		
		// Start init
		IF fbXtsEnvironment.Init(TRUE) THEN
			// stop init
			fbXtsEnvironment.Init(FALSE);
			
			// next step
			nEnvironmentState	:= 2;
		END_IF
		
	2:	// ------------------------------------------ Check for success
	
		// Check IsInitialized property
		IF fbXtsEnvironment.P_IsInitialized THEN
			// next step
			nEnvironmentState	:= 3;
		END_IF
		
	3:	// ------------------------------------------- Init Visu
	
		fbXtsEnvironmentVisu( ipTcIoXtsEnvironment := fbXtsEnvironment );
		
		// next step
		nEnvironmentState	:= 4;
		
	4:	// ------------------------------------------- Init done

		fbXtsEnvironmentVisu.Cycle();
		
END_CASE

]]></ST>
      </Implementation>
    </Action>
    <LineIds Name="MAIN">
      <LineId Id="664" Count="18" />
      <LineId Id="687" Count="81" />
      <LineId Id="770" Count="0" />
      <LineId Id="772" Count="0" />
      <LineId Id="774" Count="0" />
      <LineId Id="1393" Count="2" />
      <LineId Id="1392" Count="0" />
      <LineId Id="1401" Count="1" />
      <LineId Id="1397" Count="2" />
      <LineId Id="1396" Count="0" />
      <LineId Id="1405" Count="1" />
      <LineId Id="787" Count="2" />
      <LineId Id="803" Count="42" />
      <LineId Id="851" Count="2" />
      <LineId Id="1786" Count="0" />
      <LineId Id="854" Count="0" />
      <LineId Id="1787" Count="0" />
      <LineId Id="863" Count="0" />
      <LineId Id="1886" Count="0" />
      <LineId Id="865" Count="0" />
      <LineId Id="870" Count="13" />
      <LineId Id="1653" Count="0" />
      <LineId Id="1652" Count="0" />
      <LineId Id="1654" Count="1" />
      <LineId Id="884" Count="0" />
      <LineId Id="1300" Count="0" />
      <LineId Id="1725" Count="0" />
      <LineId Id="1301" Count="2" />
      <LineId Id="1631" Count="0" />
      <LineId Id="1306" Count="2" />
      <LineId Id="941" Count="0" />
      <LineId Id="1310" Count="0" />
      <LineId Id="1309" Count="0" />
      <LineId Id="1724" Count="0" />
      <LineId Id="1312" Count="2" />
      <LineId Id="1723" Count="0" />
      <LineId Id="1720" Count="2" />
      <LineId Id="1316" Count="3" />
      <LineId Id="1311" Count="0" />
      <LineId Id="1381" Count="1" />
      <LineId Id="1438" Count="0" />
      <LineId Id="1445" Count="0" />
      <LineId Id="1444" Count="0" />
      <LineId Id="1573" Count="1" />
      <LineId Id="1450" Count="0" />
      <LineId Id="1633" Count="0" />
      <LineId Id="1437" Count="0" />
      <LineId Id="1465" Count="0" />
      <LineId Id="1464" Count="0" />
      <LineId Id="1446" Count="0" />
      <LineId Id="942" Count="21" />
      <LineId Id="1657" Count="2" />
      <LineId Id="1656" Count="0" />
      <LineId Id="964" Count="32" />
      <LineId Id="1000" Count="0" />
      <LineId Id="1876" Count="4" />
      <LineId Id="1019" Count="12" />
      <LineId Id="1035" Count="2" />
      <LineId Id="1385" Count="0" />
      <LineId Id="1540" Count="0" />
      <LineId Id="1545" Count="0" />
      <LineId Id="1052" Count="0" />
      <LineId Id="1055" Count="1" />
      <LineId Id="1058" Count="17" />
      <LineId Id="1077" Count="0" />
      <LineId Id="1642" Count="0" />
      <LineId Id="1641" Count="0" />
      <LineId Id="1643" Count="0" />
      <LineId Id="1645" Count="0" />
      <LineId Id="1650" Count="0" />
      <LineId Id="1649" Count="0" />
      <LineId Id="1078" Count="13" />
      <LineId Id="1881" Count="0" />
      <LineId Id="1159" Count="0" />
      <LineId Id="1418" Count="0" />
      <LineId Id="1299" Count="0" />
      <LineId Id="1096" Count="0" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="MAIN.ACT">
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="MAIN.ACT_BallSeat">
      <LineId Id="2" Count="0" />
      <LineId Id="4" Count="0" />
      <LineId Id="8" Count="0" />
      <LineId Id="12" Count="0" />
      <LineId Id="17" Count="1" />
      <LineId Id="16" Count="0" />
      <LineId Id="15" Count="0" />
      <LineId Id="3" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="MAIN.ACT_IOSim">
      <LineId Id="1" Count="3" />
      <LineId Id="12" Count="0" />
      <LineId Id="11" Count="0" />
      <LineId Id="13" Count="0" />
      <LineId Id="26" Count="0" />
      <LineId Id="6" Count="3" />
      <LineId Id="5" Count="0" />
      <LineId Id="10" Count="0" />
      <LineId Id="16" Count="0" />
      <LineId Id="15" Count="0" />
      <LineId Id="17" Count="2" />
      <LineId Id="22" Count="0" />
      <LineId Id="21" Count="0" />
      <LineId Id="23" Count="0" />
      <LineId Id="29" Count="0" />
      <LineId Id="27" Count="1" />
      <LineId Id="25" Count="0" />
      <LineId Id="24" Count="0" />
    </LineIds>
    <LineIds Name="MAIN.ACT_Orient">
      <LineId Id="2" Count="0" />
      <LineId Id="4" Count="0" />
      <LineId Id="8" Count="0" />
      <LineId Id="12" Count="1" />
      <LineId Id="16" Count="0" />
      <LineId Id="15" Count="0" />
      <LineId Id="14" Count="0" />
      <LineId Id="9" Count="0" />
      <LineId Id="3" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="MAIN.ACT_Print">
      <LineId Id="2" Count="0" />
      <LineId Id="4" Count="0" />
      <LineId Id="8" Count="0" />
      <LineId Id="12" Count="0" />
      <LineId Id="19" Count="0" />
      <LineId Id="17" Count="1" />
      <LineId Id="16" Count="0" />
      <LineId Id="20" Count="1" />
      <LineId Id="15" Count="0" />
      <LineId Id="3" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="MAIN.InitializeUtilityLibrary">
      <LineId Id="2" Count="49" />
      <LineId Id="1" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>